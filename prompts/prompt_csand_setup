#
# A prompt based on Steve Losh's extravagant prompt. Uses git-info for
# repository information.
#

function prompt_csand_help {
  cat <<EOH
This prompt is designed for use on 256 colour terminals.
EOH
}

function prompt_csand_preview {
  prompt_preview_theme csand
}

function prompt_csand_precmd {
  # Get virtual environment information
  if (( $+functions[python-info] ))
  then
    python-info
  fi

  # Get Git repository information
  if (( $+functions[git-info] ))
  then
    git-info
  fi
}

function prompt_csand_preexec {
  # Emit a blank line before executing the command unless the command is
  # known to not print anything. Unfortunately, I'm not sure how to compensate
  # for AUTO_CD.
  cmd=(${=1})
  case $cmd[1] in
    'cd') ;;
    *) echo '';;
  esac
}

function prompt_csand_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions
  autoload -Uz add-zsh-hook

  # Add hooks for precmd and preexec
  add-zsh-hook precmd  prompt_csand_precmd
  add-zsh-hook preexec prompt_csand_preexec

  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'

  # git info format
  zstyle ':prezto:module:git:info:branch'    format 'branch %F{38}%b%f'
  zstyle ':prezto:module:git:info:modified'  format '%F{160}!%f'
  zstyle ':prezto:module:git:info:untracked' format '%F{172}?%f'
  zstyle ':prezto:module:git:info:added'     format '%F{82}+%f'
  zstyle ':prezto:module:git:info:ahead'     format ' up %F{178}%A%f'
  zstyle ':prezto:module:git:info:behind'    format ' down %F{105}%A%f'
  zstyle ':prezto:module:git:info:keys'      format \
    'prompt'  ' %b%m%u%a%A%B' \
    'rprompt' ''

  # virtualenv info format
  zstyle ':prezto:module:python:info:virtualenv' format ' venv %F{77}%v%f'

  # Define prompts
  PROMPT='
  %F{227}%n%f at %F{198}%m%f in %F{135}%~%f${git_info:+${(e)git_info[prompt]}}%f${python_info[virtualenv]}
  %F{15}â‡’%f '

  RPROMPT=''
}

prompt_csand_setup "$@"
